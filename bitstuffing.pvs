bitstuffing: THEORY
BEGIN
    % maximale Wiederholungen von Bits in einer "stuffed" Bitsequenz
    % Bsp: max_rep = 4
    % 100001 -> Okay da 4 wiederholende Nullen
    % 100000 -> Nicht Okay, wird von stuff-Funktion umgewandelt zu:
    % 1000010
    MAX_REP: nat

    maximal(m: nat): TYPE = {n: nat | n <= m}
    between(s: nat, e: nat): TYPE = {n: nat | s <= n AND n <= e}

    % Sequenz mit beliebiger Länge
    bit_seq: TYPE = finite_sequence[bool]    
    % Sequenz mit genau n Bits
    fix_seq(n: nat): TYPE = {s: bit_seq | s`length = n}
    % Sequenzen mit mehr als oder gleich n Bits
    min_seq(n: nat): TYPE = {s: bit_seq | s`length >= n}

    % Bedingung für Ergebnis der "stuff" Funktions
    % index aus i, so dass von alle j von i + 1 bis i + max_rep - 1
    % s(j) = s(i)
    stuffed?(s: bit_seq): bool =
    	s`length <= MAX_REP OR
	NOT EXISTS(start: below(s`length - MAX_REP)):
	    FORALL(i: between(start, start+MAX_REP)):
	        s(start) IFF s(i)

    fix_stuffed(l: nat): TYPE = {s: fix_seq(l) | stuffed?(s)}
    min_stuffed(l: nat): TYPE = {s: min_seq(l) | stuffed?(s)}

    % Wrapper um genau ein Element
    single(el: bool): fix_seq(1) =
        (# length := 1, seq := LAMBDA(i: below(1)): el #)

    % Alle Bits außer das erste
    tail(s: min_seq(1)): fix_seq(s`length-1)  =
        s ^^ (1, s`length)

    % Füge Bit als "Kopf" an eine Sequenz an
    cons(head: bool, tail: bit_seq): fix_seq(tail`length + 1) =
        single(head) o tail

    % Wenn ich Element an stuffed Liste anhänge, ist dies auch stuffed wenn:
    % 	   * tail`length < MAX_REP ist
    % Beweis über Länge: tail`length + 1 <= MAX_REP 
    cons_smaller: FORMULA
        FORALL(head: bool, tail: (stuffed?)):
	    (tail`length < MAX_REP) IMPLIES
	        stuffed?(cons(head,tail))
		
    % Wenn ich Element an stuffed Liste anhänge, ist dies auch stuffed wenn:
    %	   * head ungleich ersten Element von tail ist
    % Beweis: Head ist unterschiedlich zu tail(0) -> also muss Eigenschaft erfüllt sein
    cons_xor: FORMULA
        FORALL(head: bool, tail: min_stuffed(MAX_REP+1)):
	    (head XOR tail(0)) IMPLIES
	        stuffed?(cons(head,tail))

    % Tail von einer "stuffed" Sequenz ist immer auch Stuffed
    tail_stuffed: FORMULA
        FORALL(s: min_stuffed(1)):
	    stuffed?(tail(s))

    stuff(s: bit_seq, rep_count: nat, rep_bit: bool): RECURSIVE min_stuffed(s`length) =
        IF s`length = 0 THEN
	    empty_seq
	ELSE
	    IF rep_count >= MAX_REP AND s(0) IFF rep_bit THEN
	        cons(NOT s(0), cons(s(0), stuff(tail(s), 1, s(0)))) % Fall 1
	    ELSE
		LET new_count = IF s(0) IFF rep_bit THEN rep_count+1 ELSE 1 ENDIF IN
	            cons(s(0), stuff(tail(s), new_count, s(0))) % Fall 2
	    ENDIF
	ENDIF
    MEASURE s`length

    
    % für n > 0 gibt stuff eine Sequenz zurück, die weniger als MAX_REP gleiche Bits hat
    stuff_helper: FORMULA
        FORALL(s: min_seq(MAX_REP), n: between(1,MAX_REP), b: bool):
	    LET r = stuff(s, n, b) IN
	    NOT FORALL(i: between(0, MAX_REP-1)): 
	        r(0) IFF r(i)
	    

%    unstuff(s: (stuffed?), rep_count: nat, rep_bit: bool): RECURSIVE bit_seq =
%        IF s`length = 0 THEN
%	    empty_seq
%	ELSE
%	    IF rep_count >= MAX_REP-1 AND s(0) IFF rep_bit THEN
%	        cons(s(0), unstuff(tail(tail(s)), 0, False))
%	    ELSE
%	        LET new_count = IF s(0) IFF rep_bit THEN rep_count+1 ELSE 1 ENDIF IN
%		    cons(s(0), unstuff(tail(s), new_count, s(0)))
%    	    ENDIF
%	ENDIF
%    MEASURE s`length

%    correct: FORMULA
%        FORALL(s: bit_seq):
%	    s = unstuff(stuff(s, 0, False)`seq, 0, False)

    % Beweisidee für die tcc
    % Bei Fall 1 und Fall 2 entsteht durch das zusammenhängen eine stufffed bit_seq, denn
    % wenn wir s_0 mit stuff(s_1..n, 1, s(0)) zusammenhängen
    % beginnt stuff(s_1..n, 1, s(0)) mit max. max_rep-1 gleichen bits
    % Also entsteht durch das Zusammenhängen maximal max_rep Wiederholungen -> stuffed? ist erfüllt
    %n: VAR below(MAX_REP)
    %b: VAR bool
    %stuff_starts: FORMULA
    %    FORALL(s: min_seq(MAX_REP + 1)): %Bitsequenz bei der Stuffing nötig sein könnte   
    %	    EXISTS(i: below(MAX_REP+1-n)):	      %0..3+1-0 -> muss mindestens einer verschieden zu b sein
    % 	        NOT b IFF stuff(s, n, b)(i)
	    
    
END bitstuffing
